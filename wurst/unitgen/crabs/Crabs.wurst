package Crabs
import CrabIds
import UnitObjEditing
import LinkedListModule

class Crab
	use LinkedListModule
	UnitDefinition def
	
	int id
	int hp = 50
	int movespeed = 280
	int damage = 2
	real cooldown = 0.5
	real scale = 1.
	int range = 40
	real hpreg = 1.
	real colsize = 16
	real aqr = 1600
	Race rce = Race.Critters
	MovementType mtype = MovementType.Amphipic
	
	construct(int origId, int newId, string name)
		def = new UnitDefinition(newId, origId)
		def.setName(name)
		this.id = newId
	
	function generate()
		def..setHitPointsMaximumBase(hp)
		..setSpeedBase(movespeed)
		..setAttack1DamageBase(damage)
		..setAttack1CooldownTime(cooldown)
		..setRace(rce)
		..setScalingValue(scale)
		..setMovementType(mtype)
		..setSleeps(false)
		..setAttack1Range(range)
		..setCollisionSize(colsize)
		..setHitPointsRegenerationRate(hpreg)
		..setAcquisitionRange(aqr)
		..setSightRadiusDay(1800)
		..setSightRadiusNight(1800)

public function initCrabs(boolean ct)
	var c =  new Crab('nsc3', GECRAB_ID, "GIANT ENEMY CRAB")
	c.hp = 100000
	c.damage = 52
	c.cooldown = 2.75
	c.movespeed = 165
	c.scale = 5
	c.range = 80
	c.hpreg = 250.
	c.colsize = 32.
	c.aqr = 999999

	c = new Crab('nscb', CRABBY_ID, "Crabby")
	c.movespeed = 315
	
	
	c = new Crab('nsc2', KINGLER_ID, "Kingler")
	c.scale = 1.2
	c.hp = 120
	c.movespeed = 280
	
	c = new Crab('nqb2', PIGLET_ID, "Piglet")
	c.scale = 0.8
	c.hp = 25
	c.movespeed = 350

	if ct
		var e = Crab.first
		while e != null
			e.generate()
			e = e.next
	
